#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
GetOptions(
  'force' => \(my $force = 0),
);

my $branches = `git branch`;
my @dust = map {
  s/^\*?\s*//;
  $_;
} grep {
  $_ =~ m{dustbin/}
} split(/\n/, $branches);

chomp(my $current = `git rev-parse --abbrev-ref HEAD`);

sub normalize_branch_name {
  my ($abbrev) = @_;
  return $abbrev if $abbrev !~ /^@\{-\d+\}$/;
  chomp(my $name = qx{git rev-parse --abbrev-ref $abbrev});
  die "Couldn't convert $abbrev to a name\n" if ! $name;
  return $name;
}

sub has_branch {
  my ($branch) = @_;
  return $branches =~ m{^\*?\s*\b$branch\b\s*$}m;
}

if (@ARGV == 1 && $ARGV[0] eq 'clear' && ! has_branch($ARGV[0])) {
  if (!$force) {
    die "git-dustbin: won't `clear` without `--force`\n";
  } else {
    system { 'git' } ('git', 'branch', '-D', $_) foreach @dust;
    exit(0);
  }
}

if (! @ARGV) {
  print "$_\n" foreach @dust;
  exit;
}

foreach my $branch (@ARGV) {
  $branch = normalize_branch_name($branch);

  if ($branch eq '.' || $branch eq $current) {
    $branch = $current;
    if ($branch eq 'master') {
      warn "git-dustbin: refusing to dustbin `master`\n";
      next;
    } else {
      system { 'git' } qw(git checkout master);
    }
  }

  if ($branch eq '-') {
    chomp($branch = `git rev-parse --abbrev-ref \@{-1}`);
  }

  if ($branch eq 'master') {
    warn "git-dusbin: refusing to dustbin `master`\n";
    next;
  }

  if (has_branch($branch)) {
    system { 'git' } ('git', 'branch', '-m', $branch, "dustbin/${branch}");
    if ($? != 0) {
      warn "git-dustbin: couldn't move ${branch} to dustbin/${branch}\n";
    }
  } else {
    warn "git-dustbin: no such branch ${branch}\n";
  }
}
