#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
GetOptions(
  't|test' => \(my $just_testing = 0),
);

$|++;

if ($just_testing) {
  print "just testing...\n";
}

my $file_or_dir = shift(@ARGV);
die "usage: $0 <file_or_directory>\n" if ! $file_or_dir;

my @bad;
if (-f $file_or_dir) {
  push @bad, es6ify($file_or_dir, $just_testing);
} elsif (-d $file_or_dir) {
  my @queue = ($file_or_dir, @ARGV);

  while (my $dir = shift(@queue)) {
    print "processing $dir/\n" if $just_testing;

    opendir my $dh, $dir or die "opendir $dir: $!\n";

    while (my $file = readdir $dh) {
      next if $file eq '.' or $file eq '..';
      if (-d "$dir/$file") {
        push @queue, "$dir/$file";
      } elsif (-f "$dir/$file") {
        print "\t$dir/$file\n" if $just_testing;
        push @bad, es6ify("$dir/$file", $just_testing);
      }
    }
  
    closedir $dh;
  }
}

if (@bad) {
  use Data::Dumper;
  die "errors: " . Dumper(\@bad);
}

sub cat {
  my ($file) = @_;
  local (@ARGV, $/) = ($file);
  return (<>);
}

sub minus {
  my ($a1, $a2) = @_;
  my %s = map { ($_ => 1) } @$a2;
  return grep { !$s{$_} } @$a1;
}

sub dedup {
  my %s = ();
  return grep { ! $s{$_}++ } @_;
}

sub get_cloudui_globals {
  my ($file) = @_;

  open my $fh, '<', $file or die "$file: $!\n";
  my (@globals, @modules, @imports);
  my %imported;
  my $skip = 0;
  while (<$fh>) {
    chomp;
    next if m{//};
#    if (/^export/) {
#      $skip = 1;
#      last;
#    }

    if (/import.*from.*;/) {
      chomp(my $have = $_);
      $imported{$have}++;
      next;
    }

    if (my @match_globals = $_ =~ m{(?:CloudUI|EmberFabric)\.([A-Za-z_0-9]+)}g) {
      push @globals, grep { $_ !~ /__container__/ } @match_globals;
    }

#     if (my @match_modules = $_ =~ m{(?:CloudUI|EmberFabric)\.([A-Za-z0-9_]+)(?:\s*=.*(?:extend|create))}g) {
#       push @modules, @match_modules;
#     }

    if (my @match_modules = $_ =~ m{window.CloudUI\.([A-Za-z0-9_]+) = \1; // pseudo-export}g) {
      push @modules, @match_modules;
    }

    if (my @match_modules = $_ =~ m{var (\S+) = fabric.util.createClass}g) {
      push @modules, @match_modules;
    }

    if ($_ =~ m{Ember\.}) {
      push @imports, "import Ember from 'ember';";
    }
    if ($_ =~ m{DS\.}) {
      push(@imports, "import DS from 'ember-data';");
    }
  }
  close $fh;

  my @skiplist = qw(swfPath);
  @globals = minus(\@globals, [@modules, @skiplist]);

  @imports = minus(\@imports, [keys %imported]);

  return +{
    skip => $skip,
    globals => [dedup @globals],
    modules => [dedup @modules],
    imports => [dedup @imports],
  };
}

sub es6ify {
  my ($file, $just_testing) = @_;
  return () if $file !~ /\.js$/;

  local $@;
  my $out;
  eval { $out = _es6ify($file, $just_testing) };
  if ($@) {
    chomp($@);
    return "$file: $@";
  }

  if ($out->{not_found}) {
    return join("\n", map { "$file: $_" } @{$out->{not_found}});
  }
  return ();
}

sub _es6ify {
  my ($file, $just_testing) = @_;

  my $parse = get_cloudui_globals($file);
  return if $parse->{skip};

  my $modules = join('|', @{$parse->{modules}});

  die "insane file has no visible modules: $file\n" if ! $modules;
  die "insane file has too many modules: $file\n" if $modules =~ /\|/;

  my @imports;
  my %rename;
  if (@{$parse->{globals}}) {
    my $search = join '|', @{$parse->{globals}};
    my @deps = qx{git grep -P '(?:CloudUI|EmberFabric)\\.(?:$search) ='};
    push @deps, qx{git grep -P 'export default (?:$search);'};
    chomp @deps;

    my @not_found = ();
    my $deps = join "\n", @deps;
    foreach my $g (@{$parse->{globals}}) {
      next if $deps =~ /$g/;
      push @not_found, $g;
    }
    if (@not_found) {
      $parse->{not_found} = \@not_found;
    }

    @imports = map {
      my ($file, $full, $namespace, $module) = $_ =~ m{^(.*?)\.js:((CloudUI|EmberFabric).($search)) =};
      if (!$file) {
        ($file, $module) = $_ =~ m{^(.*?)\.js:export default ($search);};
        $full = "CloudUI.$module";
      }

      if (!$file) {
        die "couldn't figure imports for $file -- $search\n";
      }

      $rename{$full} = $module;
      "import $module from 'cloud-ui/$file';";
    } @deps;

    @imports = dedup @imports;
  }

  return if $just_testing;

  @imports = map {
    s,import computed from 'cloud-ui/helpers/computed',import computed as cloudComputed from 'cloud-ui/helpers/computed',;
    s,import RSVP from 'cloud-ui/helpers/rsvp',import { RSVP as CloudRSVP } from 'cloud-ui/helpers/rsvp',;
    $_;
  } @imports;

  $rename{'CloudUI.computed'} = 'cloudComputed';
  $rename{'CloudUI.RSVP'} = 'CloudRSVP';

  my $renames = join '|', keys(%rename);

  local $^I = '';
  local (@ARGV) = ($file);
  my @exports = ();
  my %imported;
  while (<>) {
    if (s{^(?:CloudUI|EmberFabric)\.($modules) = }{var $1 = }) {
      push @exports, $1;
    }
  
    if ($renames) {
      s/($renames)/$rename{$1}/ge;
    }

    if ($_ !~ /^require/) {
      print $_;
    }

    if (m{^// All rights reserved}) {
      if (@{$parse->{imports}}) {
        print "\n";
        print "$_\n" foreach @{$parse->{imports}};
        @{$parse->{imports}} = ();
      }
      if (@imports) {
        print "\n";
        print join("\n", @imports);
        @imports = ();
      }
    }

    # if last line, dump exports
    if (eof(ARGV)) {
      if (@exports) {
        print "\n";
        print "export default $_;\n" foreach @exports;
      }
    }
  }

  return $parse;
}
